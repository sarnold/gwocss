C*********************************************************************
        SUBROUTINE LEVWND
C*********************************************************************
C
C  INTERPOLATES MASS ADJUSTED FIELD TO ANEMOMETER HEIGHT 
C  (Z10, FOR Z INDEX=1)
C  & TO NHORIZ-1 FLAT PLANES (FOR Z INDICES 2-NHORIZ) SET AT THE 
C  HEIGHTS ZCHOOZ ABOVE THE LOWEST TERRRAIN GRID POINT IN THE COARSE 
C  GRID, AS DEFINEDTHE VARIABLE ARRAY SIGMA.  THIS SUBROUTINE ALSO 
C  CONVERTS INTERPOLATED WINDS BACK TO METEOROLOGICAL SPEEDS
C  AND ANGLES THAT CAN BE PLOTTED IF DESIRED.  LUDWIG--JANUARY 1988
C
C  MODIFIED APRIL 2002 TO INTERPOLATE OTHER VARIABLES (RICHARDSON NO.,
C  POT. TEMP. LAPSE RATE, PRESSURE, POT. TEMP, BRUNT VAISALA PERIOD) 
C  TO FLAT SURFACES AS WELL.  ALSO SIMPLIFIED LOGIC.
C
C	FLUDWIG 4/02
C
        PARAMETER (RAD2D=180./3.14159)
C
        INCLUDE 'NGRIDS.PAR'
C
        INCLUDE 'ANCHOR.CMM'
        INCLUDE 'FLOWER.CMM'
        INCLUDE 'LIMITS.CMM'
        INCLUDE 'TSONDS.CMM'
C
        LOGICAL IFXPT(NXGRD,NYGRD),DOTRPO,DIDFND
C
C  MAKE SURE EVERYTHING IS STILL HERE ON SUBSEQUENT CALLS
C
        SAVE
C
	K1=1
	Z0=ZZERO
	Z10=10.0
	NBADA=0
C
        CALL FIXWND (IFXPT,1)
C
	DO 400 IX=1,NCOL
           DO 390 IY=1,NROW
C
C  1ST LEVEL REPRESENTS A TERRAIN FOLLOWING SURFACE, Z10 M ABOVE SFC
C  EVERYWHERE.  IN ORDER TO REFLECT BOTH THE OBSERVATIONS USED TO
C  GET FIRST ESTIMATES OF ANEMOMETER LEVEL WINDS AND THE ADJUSTED 
C  WINDS ON THE FIRST ABOVE GROUND FLOW SURFACE, WE USE FOR THE LOWER
C  END OF THE INTERPOLATION, HALF THE INITIAL LEVEL 1 WIND ESTIMATE AT
C  THE HT WHERE A LOG PROFILE WOULD HAVE THE WINDS BE 50% OF THEIR 
C  MAGNITUDE AT Z=Z10.  VALUES AT THE 1ST LEVEL ABOVE GROUND ARE USED
C  FOR THE UPPER END OF THE INTERPOLATION.  FOR PTS NEAR OBS, THE 
C  WEIGHTING OF ANEMOMETER HT VALUES IS INCREASED BY USING Z FOR
C  90% OF ANEMOMETER HT VLAUE.  LAPSE RATE UNRELIABLE AT THIS LEVEL, SO
C  BULK RICHARDSON NUMBER AND BRUNT-VAISALA PERIOD NOT CALCULATED
C
	      ZAGL=Z10
	      IF (IFXPT(IX,IY)) THEN
                 BOTTOM=Z0*((Z10/Z0)**0.9)
                 U0=0.9*U(IX,IY,1)
                 V0=0.9*V(IX,IY,1)
                 W0=0.9*W(IX,IY,1)
	      ELSE
                 BOTTOM=Z0*((Z10/Z0)**0.5)
                 U0=0.5*U(IX,IY,1)
                 V0=0.5*V(IX,IY,1)
                 W0=0.5*W(IX,IY,1)
	      END IF
C
C  NOW GET VALUES TO USE FOR TOP END OF INTERPOLATION.
C
	      TOP=RHS(IX,IY,LEVBOT(IX,IY))
              U1=U(IX,IY,LEVBOT(IX,IY))
              V1=V(IX,IY,LEVBOT(IX,IY))
              W1=W(IX,IY,LEVBOT(IX,IY))
C
C  WE NOW HAVE VALUES ABOVE AND BELOW THE ANEMOMETER HEIGHT -- 
C  INTERPOLATE
C
              CALL LGNTRP(UUUF,BOTTOM,TOP,ZAGL,U0,U1)
 	      U(IX,IY,1)=UUUF
              CALL LGNTRP(VVVF,BOTTOM,TOP,ZAGL,V0,V1)
 	      V(IX,IY,1)=VVVF
              CALL LGNTRP(WWWF,BOTTOM,TOP,ZAGL,W0,W1)
 	      W(IX,IY,1)=WWWF
              IUGRAF(IX,IY,1)=NINT(100.0*U(IX,IY,1))
              IVGRAF(IX,IY,1)=NINT(100.0*V(IX,IY,1))
              IWGRAF(IX,IY,1)=NINT(100.0*W(IX,IY,1))
	      IPTGRF(IX,IY,1)=NINT(10.0*TMPKEL(IX,IY,1))
C	      IPRGRF(IX,IY,1)=NINT(100.0*SETALT(IX,IY,1))
	      PATSFC=CVT2P(SETALT(IX,IY,1),SFCHT(IX,IY)+Z10)
	      IPRGRF(IX,IY,1)=NINT(10.0*PATSFC)
C
C  DONE WITH ANEMOMETER HEIGHT VALUES, NOW INTERPOLATE TO HORIZONTAL 
C  PLANES (IZ>.
C 
	      DO 300 IZ=2,NFLAT
C  
C  GET HEIGHT OF HORIZONTAL PLANE ABOVE LOCAL GROUND LEVEL (ZAGL), THEN
C  FIND 1ST FLOW SURFACE ABOVE THIS FLAT SURFACE, MEANWHILE FILLING
C  BELOW SFC LEVELS WITH MISSING DATA VALUES.  IF ALL SURFACES ARE 
C  LOWER THAN TERRAIN AT THIS GRID POINT SET ALL LEVELS TO MISSING 
C  VALUES AND PROCEED TO NEXT GRID PT.
C
                 IF (DOFLAT) THEN
		    ZAGL=ZCHOOZ(IZ)-SFCHT(IX,IY)
		    ZMSL=ZCHOOZ(IZ)
		 ELSE
		    ZAGL=ZCHOOZ(IZ)
		    ZMSL=ZCHOOZ(IZ)+SFCHT(IX,IY)
		 END IF
C
	         IF (ZAGL.LE.Z0) THEN
		    DOTRPO=.FALSE.
C
C  THIS SURFACE BELOW GROUND;  SET VALUES TO ZERO OR MISSING
C		
                    IUGRAF(IX,IY,IZ)=0
                    IVGRAF(IX,IY,IZ)=0
                    IWGRAF(IX,IY,IZ)=0
	            IPTGRF(IX,IY,IZ)=0
	            IPRGRF(IX,IY,IZ)=0
C
	         ELSE IF (ZAGL .GE. RHS(IX,IY,NLVL)) THEN
		    DOTRPO=.FALSE.
C
C  EXRAPOLATION SURFACE ABOVE HIGHEST FLOW SURFACE.  USE VALUES
C  BASED ON THOSE ON HIGHEST SURFACE. ASSUME A LAPSE RATE OF 
C
                    IUGRAF(IX,IY,IZ)=NINT(100.0*U(IX,IY,NLVL))
                    IVGRAF(IX,IY,IZ)=NINT(100.0*V(IX,IY,NLVL))
                    IWGRAF(IX,IY,IZ)=NINT(100.0*W(IX,IY,NLVL))
		    IF (DOTHET .OR. DOPRES .OR. DOBRI .OR. DOBVPD) THEN
		       TTT=TMPKEL(IX,IY,NLVL)+DTHDZL(IX,IY,NLVL)*
     $                                     (ZAGL-RHS(IX,IY,NLVL))
		       IPTGRF(IX,IY,IZ)=NINT(10.0*TTT)
		       ATOP=SETALT(IX,IY,NLVL)
C		       IPRGRF(IX,IY,IZ)=NINT(100.0*ATOP)
		       IPRGRF(IX,IY,IZ)=NINT(10.0*CVT2P(ATOP,ZMSL))
		    END IF
C
		 ELSE IF (ZAGL.LE. Z10) THEN
		    DOTRPO=.TRUE.
C
C  INTERPOLATION SURFACE IS BELOW ANEMOMETER HEIGHT.  USE LOG PROFILE 
C  WIND VALUES FROM ANEMOMETER LEVEL.  USE ANEMOMETER LEVEL VALUES FOR 
C  OTHER PARAMETERS.
C 
		    IBR=4
		    BOTTOM=Z0
		    U0=0.0
		    V0=0.0
		    W0=0.0
		    IF (DOTHET .OR. DOPRES .OR. DOBRI .OR. DOBVPD) THEN
		       T0=TMPKEL(IX,IY,1)
		       T1=T0
C
C ASSUME AT LOWEST SURFACE  APPLIES HERE.
C
		       ALT0=SETALT(IX,IY,LEVBOT(IX,IY))
C		       IPRGRF(IX,IY,IZ)=NINT(100.0*ALT0)
		       IPRGRF(IX,IY,IZ)=NINT(10.0*CVT2P(ALT0,ZMSL))
		    END IF
C
C  NOW GET VALUES TO USE FOR TOP END OF INTERPOLATION.  ASSUME NO 
C  CHANGE AND USE SAME VALUES FOR THE VARIABLES OTHER THAN WIND.
C 
		    TOP=Z10
		    U1=U(IX,IY,1)
		    V1=V(IX,IY,1)
		    W1=W(IX,IY,1)
C
	         ELSE IF (ZAGL .LT.  RHS(IX,IY,LEVBOT(IX,IY))
     $                                     .AND. ZAGL.GT.Z10) THEN
		    DOTRPO=.TRUE.
C
C  THIS INTERPOLATION SFC MUST BE BETWEEN ANEMOMETER AND THE
C  LOWEST ABOVE-GROUND FLOW SURFACE.
C
		    IBR=2
C
C  GET VALUES TO USE FOR BOTTOM END OF INTERPOLATION.
C
		    BOTTOM=Z10
                    U0=U(IX,IY,1)
                    V0=V(IX,IY,1)
                    W0=W(IX,IY,1)
		    IF (DOTHET .OR. DOPRES .OR. DOBRI .OR. DOBVPD) THEN
		       T0=TMPKEL(IX,IY,1)
		       T1=TMPKEL(IX,IY,LEVBOT(IX,IY))
		       ALT0=SETALT(IX,IY,LEVBOT(IX,IY))
C		       IPRGRF(IX,IY,IZ)=-NINT(100.0*ALT0)
		       IPRGRF(IX,IY,IZ)=NINT(10.0*CVT2P(ALT0,ZMSL))
		    END IF
C
C  NOW GET VALUES TO USE FOR TOP END OF INTERPOLATION.
C
                    TOP=RHS(IX,IY,LEVBOT(IX,IY))
                    U1=U(IX,IY,LEVBOT(IX,IY))
                    V1=V(IX,IY,LEVBOT(IX,IY))
                    W1=W(IX,IY,LEVBOT(IX,IY))
C
		 ELSE IF (ZAGL.GE. RHS(IX,IY,LEVBOT(IX,IY)) 
     $                        .AND. ZAGL .LT. RHS(IX,IY,NLVL)) THEN
		    DOTRPO=.TRUE.
C
C  GETTING VALUES WHEN INTERPOLATION SFC IS BETWEEN THE LOWEST AND 
C  THE HIGHEST FLOW SFCS
C
		    DO 130 LL=LEVBOT(IX,IY),NLVL-1
		       DIDFND=.FALSE.
		       IF (ZAGL.GE.RHS(IX,IY,LL) .AND.
     $                                 ZAGL.LT.RHS(IX,IY,LL+1)) THEN                                
C
C  FOR INTERPOLATION SFC BETWEEN 2 FLOW SFCS
C
		          IBR=3
                          BOTTOM=RHS(IX,IY,LL)
                          U0=U(IX,IY,LL)
                          V0=V(IX,IY,LL)
                          W0=W(IX,IY,LL)
C
C  WIND VALUES TO USE FOR TOP END OF INTERPOLATION.
C
                          TOP=RHS(IX,IY,LL+1)
                          U1=U(IX,IY,LL+1)
                          V1=V(IX,IY,LL+1)
                          W1=W(IX,IY,LL+1)
C
C  USE WEIGHTED ALTIMETER SETTING FROM FLOW SFCS.
C
	                  IF (ZAGL.EQ.RHS(IX,IY,LL)) THEN
                             ALT0=SETALT(IX,IY,LL)
			  ELSE IF (RHS(IX,IY,LL+1) .EQ. ZAGL) THEN
			     ALT0=SETALT(IX,IY,LL+1)
			  ELSE
	                     WT1=1.0/ABS(ZAGL-RHS(IX,IY,LL))
	                     WT2=1.0/ABS(ZAGL-RHS(IX,IY,LL+1))
			     ALT0=(WT1*SETALT(IX,IY,LL)+
     $                              WT2*SETALT(IX,IY,LL+1))/(WT1+WT2)
			  END IF
C		          IPRGRF(IX,IY,IZ)=NINT(100.0*ALT0)
		          IPRGRF(IX,IY,IZ)=NINT(10.0*CVT2P(ALT0,ZMSL))
			  IF (DOTHET .OR. DOPRES .OR. DOBRI
     $                                       .OR. DOBVPD) THEN
			     T0=TMPKEL(IX,IY,LL)
			     T1=TMPKEL(IX,IY,LL+1)
			  END IF
C
C WE FOUND WHERE IT IS, WE CAN GO ON TO THE NEXT STEP
C
			  DIDFND=.TRUE.
	               END IF
		       IF (DIDFND) GO TO 135
130	            CONTINUE
135	            CONTINUE
C
		 END IF
C
C  IF WE HAVE NOT ALREADY DEFINED THE VALUES, THEN WE NEED 
C  TO INTERPLATE, USING VALUES DEFINED ABOVE.
C
		 IF (DOTRPO) THEN
C
C  IF ONE BVP VALUE, GOOD USE IT
C
                    CALL LGNTRP(UUUF,BOTTOM,TOP,ZAGL,U0,U1)
                    CALL LGNTRP(VVVF,BOTTOM,TOP,ZAGL,V0,V1)
                    CALL LGNTRP(WWWF,BOTTOM,TOP,ZAGL,W0,W1)
	            IUGRAF(IX,IY,IZ)=NINT(100.0*UUUF)
	            IVGRAF(IX,IY,IZ)=NINT(100.0*VVVF)
	            IWGRAF(IX,IY,IZ)=NINT(100.0*WWWF)
C
C  VARIABLES ARE SCALED TO INTEGERS FOR WRITING FILES.  UNITS ARE
C  CM/SEC FOR WIND COMPONENTS, (DEG K)/10 FOR POTENTIAL TEMP., MB (HP) 
C  FOR PRESSURE, SECONDS FOR BRUNT VAISALA PERIOD, AND 100* BULK 
C  RICHARDSON NO.
C
		    AASET=XLINTR(ALT0,ALT1,ZAGL,BOTTOM,TOP)
		    IF (DOTHET .OR. DOPRES .OR. DOBRI.OR. DOBVPD) 
     $                  IPTGRF(IX,IY,IZ)=NINT(10.0*
     $                              XLINTR(T0,T1,ZAGL,BOTTOM,TOP))          

		 END IF
C
300	      CONTINUE
C
390	   CONTINUE
400	CONTINUE
C
C  CONVERT COMPONENTS IN HORIZONTAL OR TERRAIN FOLLOWING 
C  PLANES TO METEOROLOGICAL WINDS SPEED & DIRECTION 
C
	DO 500 LEV=1,NFLAT
      	   DO 490 IX=1,NCOL
	      DO 480 IY=1,NROW
C
C   CONVERT INTEGER CM/S BACK TO REAL M/S
C
                 UG=0.01*FLOAT(IUGRAF(IX,IY,LEV))
                 VG=0.01*FLOAT(IVGRAF(IX,IY,LEV))
C
                 IF (UG.EQ.ZERO.AND.VG.EQ.ZERO) THEN
                    SPDMET(IX,IY,LEV)=ZERO
                    DIRMET(IX,IY,LEV)=ZERO
                 ELSE
                    SPDMET(IX,IY,LEV)=SP(UG,VG)
                    DIRMET(IX,IY,LEV)=DD(UG,VG)
                 END IF
480	      CONTINUE
490	   CONTINUE
500	CONTINUE	   
C
	RETURN	   
C
	END

