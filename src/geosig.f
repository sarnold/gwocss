C*********************************************************************
        SUBROUTINE GEOSIG
C*********************************************************************
C
C  PREPARE SFC STATION REPORTS OF WIND DIRECTION, WIND SPEED
C  (M/S), SEA LEVEL PRESSURE (MB), AND TEMPERATURE (DEG CELSIUS)
C  FOR INPUT TO WIND ANALYSIS IF NO UPPER WINDS.
C  COMPUTE GEOS WIND FROM PRESSURE AT THREE STATIONS AND
C  CORRECT IT FOR THERMAL WIND COMPONENT (IF DESIRED).
C
C  BY R M  ENDLICH, SRI INTN'L, MENLO PARK CA 94025 DEC '84.
C  VARIABLES.GEOSTROPHIC WIND CALCULATIONS WERE PUT IN THE SUBROUTINE
C  GEOSTR AND A  DIFFERENT METHOD OF WIND INTERPOLATION WAS INTRODUCED 
C  JANUARY 1988 BY F. LUDWIG.
C
C  FURTHER MODIFIED MAY 1989 TO REINTERPOLATE TO FLOW SURFACES WITH
C  SECOND CALLS TO DOPSIG, TOPWND, SFCTRP AND BETWIN --F. LUDWIG
C
C  FURTHER MODIFIED NOVEMBER 1989 TO INTERPOLATE SFC TEMPERATURES AND 
C  UPPER LEVEL POTENT TEMP LAPSE RATES TO FLOW SURFACES
C    -- F. LUDWIG
C
C  FURTHER MODIFIED MARCH 1997 TO READ A LONG SEQUENCE OF INPUTS FROM
C  THE SAME FILE (UNIT 12), NOT WINDS AND UPPER TEMPERATURES FROM 
C  SEPARATE FILES
C
C  FURTHER MODIFIED 12/01 TO READ DIFFERENT FORMAT AS USED WITH 
C  VTMX SLC DATA
C
C
C             -- F. LUDWIG  12/2001
C
C  VARIABLES:
C    NUMNWS = NUMBER OF SFC REPORTS
C    WD = WIND DIRECTION (DEG CW FROM N-- METEOROL CONVENT.)
C    SP = WIND SPEED (M/S)
C    STLT = STATION LATITUDE IN DEGS AND HUNDREDTHS
C    STLN = STATION LONGITUDE IN DEGS AND HUNDREDTHS
C    PRESS = STATION SEA LEVEL PRESSURE IN MB
C    TEMP = STATION TEMP IN DEG CELSIUS
C    SPDCNV=CONVERSION FACTOR TO CONVERT SPEEDS TO M/S, IF IN OTHER 
C           UNITS
C    DEG2R= FACTOR TO CONVERT DEGREES TO RADIANS
C
          INCLUDE 'NGRIDS.PAR'
C
        PARAMETER (PI=3.1415927,DEG2R=PI/180.0)
C
          INCLUDE 'ANCHOR.CMM'
          INCLUDE 'FLOWER.CMM'
          INCLUDE 'LIMITS.CMM'
          INCLUDE 'STALOC.CMM'
C
       INTEGER NCALLZ
C
       	SAVE
C
	DATA NCALLZ /0/
C
C  READ SURFACE STATION DATA: STATION ID, STATION COORDINATES (UTM)
C  PRESSURE, TEMPERATURE, WIND DIRECTION, WIND SPEED (MPS) 
C  READ NUMBER OF SFC OBS,NUMBER OF UPPER WIND SITES & NUMBER 
C  OF TEMP, PROFILES.  CHECK FOR ARRAY SIZES
C
	READ (12,*) NUMNWS, NUMDOP, NUMTMP
	IF (NUMNWS.GT.NSITES .OR. NUMNWS.LT.1) THEN
           WRITE (*,*) 'BAD NUMBER OF SFC OBS'
           WRITE (*,*) NUMNWS, NTSITES
     	END IF
	IF (NUMDOP.GT.NWSITE .OR. NUMDOP.LT.1) THEN
           WRITE (*,*) 'BAD NUMBER OF WIND PROFILES'
           WRITE (*,*) NUMDOP,NWSITE
	   STOP
	END IF
	IF (NUMTMP.GT.NTSITE .OR. NUMTMP.LT.1) THEN
           WRITE (*,*) 'BAD NUMBER OF TEMPERATURE PROFILES'
           WRITE (*,*) NUMTMP,NTSITE
	   STOP
	END IF
C
C  SKIP COLUMN HEADINGS FOR SURFACE DATA
C
	READ (12,*)
C
C  READ SURFACE STATION DATA -- NEW FORMAT FOR SLC VTMX 12/01
C
	DO 75 IT=1,NUMNWS
	   IF (IT.LE.NSITES) THEN
              READ (12,6004) CHSTID(IT),XS,YS,ZG(IT),PRESS,TEMPC(IT),
     $                      WD,WS,YLAT,XLONG,TEMPF,PMSL,ALTIM(IT)
C
C  CONVERT PRESS ELEVATION INFO TO ALTIMETER SETTING, THIS FIXES THOSE
C  AUTOMATED SITES WHERE RELATIVE HUMITY IS GIVEN IN THE ALTIMETER 
C  SETTING DATA FIELD.
C
	      IF (PRESS.GT.0.0) THEN
	         ALTIM(IT)=ALTSET(ZG(IT),PRESS)            
	      ELSE
	         ALTIM(IT)=-9999.0
	      END IF
C
C  CONVERTING UTM COORDINATES (KM) AND ELEV. (M) TO GRID COORDINATES
C  (KM).   NOTE THAT ORIGIN IS AT POINT 1,1
C
	      XG(IT)=1.0+(XS-XORIG)/DSCRS
              YG(IT)=1.0+(YS-YORIG)/DSCRS
	      IXG=NINT(XG(IT))
	      IYG=NINT(YG(IT))
C
C CONVERTING UNITS & GETTING COMPONENTS
C
	       IF (WS .GE. -9998.9) THEN
	 	  JGOOD(IT)=.TRUE.
              	  WS=WS*SPDCNV
                  UCOMP(IT)=-WS*SIN(WD*DEG2R)
                  VCOMP(IT)=-WS*COS(WD*DEG2R)
               ELSE
	 	  JGOOD(IT)=.FALSE.
                  UCOMP(IT)=-9999.0
                  VCOMP(IT)=-9999.0
               END IF
	   END IF
75      CONTINUE
C
	NCALLZ=NCALLZ+1
C
C  INTERPOLATE TO LOWEST ABOVE GROUND GRID POINTS, USING SFC DATA.
C
	NSFC=1
        CALL SFCTRP(NSFC)
C
C GET TOP WINDS BY INTERPOLATION BETWEEN UPPER SOUNDINGS IF AVAILABLE
C
        ICALL=1
        CALL DOPSIG (ICALL)
        CALL TOPWND
C
C  READ TEMP PROFILE & GET LAPSE RATES AT FLOW LEVELS
C
        CALL STRAT
C
        CALL BETWIN
C
C  RESHAPE THE FLOW SURFACES USING THE 1ST ESTIMATE WINDS &
C  TEMPERATURE PROFILES.
C
        CALL RESIG
C
C  GO BACK AND REINTERPOLATE TO NEW SURFACES
C
C  -- THE FOLLOWING CODE ADDED MAY 1989 TO GIVE BETTER VALUES 
C     FOR 1ST GUESS FIELD, F. LUDWIG
C
        ICALL=2
C
        CALL DOPSIG(ICALL)
        CALL TOPWND
	NSFC=2
        CALL SFCTRP(NSFC)
        CALL BETWIN
C
        DO 350 J=1, NROW
           DO 325 I=1, NCOL
              DO 300 LV=2,NLVL
C
C  WHEN RHS NEGATIVE (BELOW TERRAIN) MAKE WINDS 0.
C
                 IF (RHS(I,J,LV).LE.Z0) THEN
                    U(I,J,LV)=0.0
                    V(I,J,LV)=0.0
                 END IF
300 	      CONTINUE
325 	   CONTINUE
350 	CONTINUE
C	   
6004	FORMAT (A5,12F10.2)
C
	RETURN
C
        END

