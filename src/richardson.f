C******************************************************************
	SUBROUTINE GETRICH
C******************************************************************
C
C  GETS NECESSARY WIND SHEAR AND POTENTIAL TEMPERATURE GRADIENTS AT
C  EACH GRID POINT FROM DIFFERENCES AND INTERPOLATION ON EACH SURFACE.
C  FROM THESE THE RICHARDSON NUMBER IS ESTIMATED.  VALUES OF POTENT.
C  TEMP. AND PRESSURE ARE ALSO ESTIMATED
C
C	FLUDWIG 3/02
C
	INCLUDE 'NGRIDS.PAR'
C
	INCLUDE 'ANCHOR.CMM'
	INCLUDE 'FLOWER.CMM'
	INCLUDE 'LIMITS.CMM'
        INCLUDE 'STALOC.CMM'
	INCLUDE 'TSONDS.CMM'
C
	REAL HTDIF,TMPDIF,TMPAVG,SQDIF
C
C  WE NOW HAVE TOP AND BATTOM EVERYWHERE VALUES FOR ALTIMETER SETTING
C  AND POTENTIAL TEMPERATURE.  NOW FILL IN BETWEEN VALUES.
C
	DO 180 IX=1,NXGRD
	   DO 160 IY=1,NYGRD
     	       DO 140 IZ=1,NFLAT
C
C  IDENTIFY GRID POINTS AT OR BELOW 20 M ABOVE SFC AND FLAG HEIGHT
C  DIFFERENCE AS NEGATIVE FOR FUTURE SKIPPING.  GET DIFFERENCE IN 
C  HEIGHT BETWEEN SURFACES TO BE USED FOR CALCULATING BRUNT VAISALA 
C  AND RICHARDSON VALUES
C
		ZBLOW=100.0
	         ZATXY=SFCHT(IX,IY)+2.0*Z10
	         IF (IZ.EQ.1)  THEN
		    HTDIF=-9.0
		 ELSE IF (IZ.EQ.2) THEN
		    ZBLOW=ZCHOOZ(2)
		    HTDIF=ZCHOOZ(3)-ZCHOOZ(2)
		 ELSE IF (IZ.GT.2 .AND. IZ.LT.NFLAT) THEN
		    ZBLOW=ZCHOOZ(IZ-1)
		    HTDIF=ZCHOOZ(IZ+1)-ZCHOOZ(IZ-1)
		 ELSE IF (IZ.EQ.NFLAT) THEN
		    ZBLOW=ZCHOOZ(NFLAT-1)
		    HTDIF=ZCHOOZ(NFLAT)-ZCHOOZ(NFLAT-1)
		 END IF
C
C  FLAG SFC BELOW GROUND
C
		 IF (DOFLAT .AND. ZBLOW .LT. ZATXY) HTDIF=-9.
C
C  GRADIENTS UNRELIABLE AT ANEMOMETER LEVEL.  SET BULK RICHARDSON 
C  NUMBER & BRUNT VAISALA PERIOD AS MISSING WHEN FLAGGED HTDIF < 0.
C
	         IF (HTDIF .LT. 2.0*Z10) THEN
	            IRNGRF(IX,IY,IZ)=-2
	            IBVGRF(IX,IY,IZ)=0
		 ELSE
		    IF (IZ.EQ.NFLAT) THEN
		       TMPDIF=0.1*FLOAT(IPTGRF(IX,IY,NFLAT)-
     $                                  IPTGRF(IX,IY,NFLAT-1))
		       TMPAVG=0.05*FLOAT(IPTGRF(IX,IY,NFLAT)+
     $                                  IPTGRF(IX,IY,NFLAT-1))
		       IUDIF=IUGRAF(IX,IY,NFLAT)-IUGRAF(IX,IY,NFLAT-1)
		       IVDIF=IVGRAF(IX,IY,NFLAT)-IVGRAF(IX,IY,NFLAT-1)
		    ELSE IF (IZ.EQ.2) THEN
		       TMPDIF=0.1*FLOAT(IPTGRF(IX,IY,3)-
     $                                         IPTGRF(IX,IY,2))
		       TMPAVG=0.05*FLOAT(IPTGRF(IX,IY,3)+
     $                                         IPTGRF(IX,IY,2))
		       IUDIF=IUGRAF(IX,IY,3)-IUGRAF(IX,IY,2)
		       IVDIF=IVGRAF(IX,IY,3)-IVGRAF(IX,IY,2)

		    ELSE 
		       TMPDIF=0.1*FLOAT(IPTGRF(IX,IY,IZ+1)-
     $                                      IPTGRF(IX,IY,IZ-1))
		       TMPAVG=0.05*FLOAT(IPTGRF(IX,IY,IZ+1)+
     $                                      IPTGRF(IX,IY,IZ-1))
		       IUDIF=IUGRAF(IX,IY,IZ+1)-IUGRAF(IX,IY,IZ-1)
		       IVDIF=IVGRAF(IX,IY,IZ+1)-IVGRAF(IX,IY,IZ-1)
		    END IF
C
C  SQUARE SPEED DIFFERENCE & CONVERT FROM (CM/S)**2 TO (M/S)**2
C
     	            SQDIF=FLOAT(IUDIF**2)+FLOAT(IVDIF**2)
		    SQDIF=1.0E-4*SQDIF
C
C  NOW GET BRUNT VAISALA PERIOD AND BULK RICHARDSON NUMBER
C
	           IBVGRF(IX,IY,IZ)=NINT(BVPERD(HTDIF,TMPDIF,TMPAVG))
	           IF (IBVGRF(IX,IY,IZ) .GT. 900)  
     $                              IBVGRF(IX,IY,IZ)=900
	           IRNGRF(IX,IY,IZ)=
     $                   NINT(1000.0*BULKRI(HTDIF,TMPDIF,TMPAVG,SQDIF))
		 END IF
C
140	       CONTINUE
160	   CONTINUE
180	CONTINUE
C
	RETURN
C
	END
C
C******************************************************************
	REAL FUNCTION BVPERD (HTDIF,TMPDIF,TMPAVG)
C******************************************************************
C
C  RETURNS INVERSE OF BRUNT-VASALA FREQUENCY, CALCULATED FROM
C
C 	TMPAVG	= AVERAGE POTENT. TEMP IN LAYER
C	TMPDIF	= POTENTIAL TEMPERATURE DIFFERENCE OVER HTDIF
C	HTDIF	= THICKNESS OF LAYER
C	GRAV	= GRAVITATIONAL ACCELERATION
C
C  POTENTIAL TEMP DIFFERENCE MUST BE > 0.  IF LESS, SET PERIOD
C  TO 999 SEC.
C
C	FLUDWIG 10/02
C
	PARAMETER (GRAV=9.8)
C
	REAL HTDIF,TMPDIF,TMPAVG,BV2
C
	IF (TMPDIF.GT. 0.0) THEN
	   BV2=HTDIF*TMPAVG/(GRAV*TMPDIF)
	   BVPERD=SQRT(BV2)
	ELSE
	   BVPERD=999.
	END IF
C
	RETURN
C
	END
C
C******************************************************************
	REAL FUNCTION BULKRI(HTDIF,TMPDIF,TMPAVG,SQDIF)
C******************************************************************
C
C  RETURNS BULK RICHARDSON NUMBER, CALCULATED FROM
C
C 	TMPAVG	= AVERAGE POTENT. TEMP IN LAYER
C	SQDIF	= DU**2 + DV**2 -- SQUAREDWND DIFFERENCE OVER HTDIF
C	TMPDIF	= POTENTIAL TEMPERATURE DIFFERENCE OVER HTDIF
C	HTDIF	= THICKNESS OF LAYER
C	GRAV	= GRAVITATIONAL ACCELERATION
C
C   -0.99 RETURNED FOR ZERO WIND SHEAR.
C
C	FLUDWIG 10/02
C
	PARAMETER (GRAV=9.8)
C
	REAL HTDIF,TMPDIF,TMPAVG,SQDIF
C
	IF (TMPAVG*SQDIF .GT. 0.0) THEN
	   BULKRI=(GRAV*TMPDIF*HTDIF)/(TMPAVG*SQDIF)
	   IF (BULKRI .GT. 99.0) BULKRI=99.0
	ELSE
	   BULKRI=-0.002
	END IF
C
	RETURN
C
	END

