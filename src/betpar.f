C*********************************************************************
        SUBROUTINE BETPAR
C*********************************************************************
C
C  THIS SUBROUTINE ESTIMATES VARIABLE VALUES BETWEEN THE TOP AND 
C  BOTTOM LEVELS.   THE DEVIATION OF OBSERVED VALUE VAR FROM A LINEAR
C  PROFILE IS FIRST DETERMINED.  THEN THE DEVIATIONS AT EACH LEVEL 
C  ARE INTERPOLATED BY AN INVERSE DISTANCE TO A POWER (PWR) WEIGHTING 
C  SCHEME.  THE INTERPOLATED DEVIATIONS ARE USED TO CORRECT  THE 
C  CALCULATED PROFILES AT THE GRID POINTS.
C
C               --F  LUDWIG  6/2002
C
	INCLUDE 'NGRIDS.PAR'
C       
        PARAMETER (PWR=-2.0)
C
	INCLUDE 'ANCHOR.CMM'
	INCLUDE 'FLOWER.CMM'
	INCLUDE 'LIMITS.CMM'
	INCLUDE 'STALOC.CMM'
	INCLUDE 'TSONDS.CMM'
C
	REAL VARBL1(NSITES),VARBL2(NSITES),VARBL3(NSITES)
	REAL XX(NSITES),YY(NSITES),ATEMP(NXGRD,NYGRD)

	LOGICAL BYSOND(NXGRD,NYGRD)
C
C  LOCAL VARIABLES:
C	VARBLJ(I)=	TEMPORARY STORAGE OF VALUES FROM SOUNDINGS TO 
C	         	BE USED FOR INTERPOLATION OF:
C                  		POTENT. TEMP. 			J=1
C                  		POTENT. TEMP. LAPSE RATE 	J=2
C                  		ALTIMETER SETTING	 	J=3
C	XX,YY =		TEMPORARY STORAGE OF SOND SITE COORDINATES
C	ATEMP =		TEMPORARY STORAGE OF 2-D GRIDDED VALUES 
C	BYSOND =	FLAG FOR GRID PTS BY SONDS	
C
C  CHECK FOR ENOUGH SOND INFO
C
	IF (NUMTMP .LE. 0 .OR. NLVL .LE. 3) THEN
           WRITE(*,*) ' ONLY ',NLVL-1,' FLOW SURFACES'
           WRITE(*,*) ' ONLY ',NUMTMP,' T-SONDES'
           WRITE(*,*) 'CANNOT WORK '
           STOP
        END IF
C
C  INTIALIZE BYSOND TO REFLECT GRID PTS BY TEMPERATURE SONDS THAT ARE TO
C  BE HELD FIXED.
C
	DO 22 IX=1,NXGRD
	   DO 20 IY=1,NYGRD
	      DO 17 IT=1,NUMTMP
		 IF (IX.EQ.NINT(XTMP(IT)) .AND. 
     $                       IY.EQ.NINT(YTMP(IT))) THEN
                    BYSOND(IX,IY)=.TRUE.
		 ELSE
		    BYSOND(IX,IY)=.FALSE.
		 END IF
17	      CONTINUE
20	   CONTINUE
22	CONTINUE
C
C  ESTIMATE POTENTIAL TEMP LAPSE RATE BY HORIZONTAL INTERPOLATION 
C  BETWEEN SOUNDINGS.
C
	DO 60 IX=1,NCOL
	   X0=FLOAT(IX)
	   DO 55 IY=1,NROW
	      Y0=FLOAT(IY)
	      DO 50 IZ=2,NLVL
C
	         DO 40 IT=1,NUMTMP
		       XX(IT)=XTMP(IT)
		       YY(IT)=YTMP(IT)
	               VARBL2(IT)=PTLAPS(IT,IZ)
40	         CONTINUE
	         CALL RINVMOLD(TRPVAL,X0,Y0,XX,YY,NUMTMP,VARBL2)
	         DTHDZL(IX,IY,IZ)=TRPVAL
C
50	      CONTINUE
55	   CONTINUE
60	CONTINUE
C
C  ESTIMATE ALTIMETER SETTING ON FLOW GRID
C
	DO 200 IX=1,NCOL
	   X0=FLOAT(IX)
	   DO 195 IY=1,NROW
	      Y0=FLOAT(IY)
	      DO 190 IZ=2,NLVL
	         JKIT=0
C
C  GET ALTIMETER SETTING AT SONDS
C
	         DO 105 IT=1,NUMTMP
C
C  USE ONLY NON-RASS SONDS
C
		    IF (GOODT(IT)) THEN
	               JKIT=JKIT+1
		       XX(JKIT)=XTMP(IT)
		       YY(JKIT)=YTMP(IT)
	               VARBL3(JKIT)=ALTSIG(IT,IZ)
		     END IF
105		 CONTINUE
	         NUM3USE=JKIT
C
C  IF NO NON-RASS UPPER LEVEL PRESURE INFO, USE SFC VALUES
C
		 IF (NUM3USE.EQ.0) THEN
	            DO 110 IS=1,NUMNWS
		       IF (MNX .GT. 0 .AND. MNX .LE. NXGRD .AND. 
     $                         MNY.GT.0 .AND. MNY .LE. NYGRD) THEN     	            
     	                  IF (ALTIM(IS).GT.-9998.9) THEN
     	                     NUM3USE=NUM3USE+1
			     XX(NUM3USE)=XG(IS)
			     YY(NUM3USE)=YG(IS)
			     VARBL3(NUM3USE)=ALTIM(IS)
			  END IF
		       END IF
C		       
110		    CONTINUE
		 END IF
C
C  CHECK FOR STATIONS ON GRID AT HEIGHTS NEAR THE FLOW SURFACE TO BE
C  ADDED TO THE LIST
C
	         IF (NUM3USE.GT.0) THEN
	            CALL RINVMOLD(TRPVAL,X0,Y0,XX,YY,NUM3USE,VARBL3)
	            SETALT(IX,IY,IZ)=TRPVAL
		    PRMB(IX,IY,IZ)=CVT2P(SETALT(IX,IY,IZ),
     $                             SFCHT(IX,IY)+RHS(IX,IY,IZ))
		 ELSE
		    PRMB(IX,IY,IZ)=0.0
		 END IF
190	      CONTINUE
195	   CONTINUE
200	CONTINUE
C
C  ESTIMATE POTENTIAL TEMPERATURE ON FLOW GRID
C
	DO 300 IX=1,NCOL
	   X0=FLOAT(IX)
	   DO 295 IY=1,NROW
	      Y0=FLOAT(IY)
	      DO 290 IZ=2,NLVL
C
C  GET POTENTIAL TEMPS AT SONDS
C
	         DO 205 IT=1,NUMTMP
		    XX(IT)=XTMP(IT)
		    YY(IT)=YTMP(IT)
	            VARBL1(IT)=PTSIGL(IT,IZ)
205		 CONTINUE
	         NUM1USE=NUMTMP
C
	         CALL RINVMOLD(TRPVAL,X0,Y0,XX,YY,NUM1USE,VARBL1)
	         TMPKEL(IX,IY,IZ)=TRPVAL
290	      CONTINUE
295	   CONTINUE
300	CONTINUE
C
C  NOW GET 1ST-GUESS TEMPERATURE AND ALTIMETER AT ANEMOMETER HEIGHT BY 
C  INTERPOATION OF SURFACE OBS.
C
	CALL VRSMOO (ALTIM,ATEMP,NUMNWS)
	DO 310 IX=1,NCOL
	  DO 310 IY=1,NROW
	    SETALT(IX,IY,1)=ATEMP(IX,IY)
	    PRMB(IX,IY,1)=CVT2P(SETALT(IX,IY,1),SFCHT(IX,IY)+Z10)
310	CONTINUE
C
	CALL VRSMOO (TEMPC,ATEMP,NUMNWS)
	DO 320 IX=1,NCOL
	  DO 320 IY=1,NROW
	    TMPKEL(IX,IY,1)=TPOT(PRMB(IX,IY,1),ATEMP(IX,IY))
320	CONTINUE
C
C  FINALLY GO BACK AND CHECK FOR BELOW GROUND SFCS, ALSO REPLACE
C  UPPER LEVEL SFC LEVEL INTERPOLATIONS (WHICH ARE UNRELIABLE) WITH
C  ESTIMATES FROM UPPER LEVEL SOND INTERPOLATION.
C
	DO 400 IX=1,NCOL
	   DO 395 IY=1,NROW 
	      DTHDZL(IX,IY,1)=DTHDZL(IX,IY,LEVBOT(IX,IY))
	      DO 390 IZ=2,LEVBOT(IX,IY)
C
C  POINTS BELOW GROUND ARE SET TO ZERO
C
	         IF (IZ.LT.LEVBOT(IX,IY)) THEN
		    DTHDZL(IX,IY,IZ)=0.0
		    SETALT(IX,IY,IZ)=0.0
	            PRMB(IX,IY,IZ)=0.0
	            TMPKEL(IX,IY,IZ)=0.0
C
C  IF FLOW SFC 2 IS NOT THE FIRST SURFACE ABOVE GROUND, ESTIMATE 
C  ANEMOMETER HEIGHT VALUES FROM THE FLOW SFC VALUES.
C		    
		 ELSE
		    IF (IZ.GT.2) THEN
		       SETALT(IX,IY,1)=SETALT(IX,IY,IZ)
	               PRMB(IX,IY,1)=CVT2P(SETALT(IX,IY,IZ),
     $                                        SFCHT(IX,IY)+Z10)
     	               TMPKEL(IX,IY,1)=TMPKEL(IX,IY,IZ)-
     $                           (RHS(IX,IY,IZ)-Z10)*DTHDZL(IX,IY,IZ)
		    END IF
		 END IF
390	      CONTINUE	      
395	   CONTINUE
400	CONTINUE
C
        RETURN
C
        END

