C*********************************************************************
        SUBROUTINE RESIG
C*********************************************************************
C
C  REDEFINES THE HEIGHTS OF THE FLOW SURFACES BASED ON WIND SPEED OVER
C  THE LOWEST TERRRAIN HEIGHTS & LAPSE RATES AT THE VARIOUS FLOW LEVELS
C  --THE UNDERLYING CONCEPT IS SIMILAR TO THAT OF THE 'CRITICAL STREAM-
C  LINE'. WEIGHTED AVERAGES ARE USED TO CALCULATE FLOW SURFACE HEIGHTS 
C  AT EACH GRID POINT, GIVING GREATEST WEIGHT TO VALUES APPROPRIATE TO 
C  THE NEAREST SOUNDINGS. 
C
C    --LUDWIG, JANUARY 1988.
C
          INCLUDE 'NGRIDS.PAR'
C
          INCLUDE 'ANCHOR.CMM'
          INCLUDE 'FLOWER.CMM'
          INCLUDE 'LIMITS.CMM'
          INCLUDE 'STALOC.CMM'
          INCLUDE 'TSONDS.CMM'
C
       REAL COMPDZ
C
C  MAKE SURE STUFF IS HERE WHEN YOU COME BACK
C
        SAVE
C
C  OVERS(DPTDZ,DZTOP) IS A STATEMENT FUNCTION THAT CAN BE USED TO 
C  DEFINE MAXIMUM RISE OF A FLOW SURFACE IN TERMS OF THE POTENTIAL 
C  TEMPERATURE LAPSE RATE (DPTDZ) RATE AND THE MAXIMUM DIFFERENCE IN 
C  TERRAIN ELEVATION (DZMAX). THE VERSION INCLUDED HERE ASSUMES  THAT 
C  FLOW FOLLOWS UNDER-LYING TERRAIN FOR NEUTRAL OR UNSTABLE CONDITIONS 
C  & THAT THERE IS NO OVERSHOOT OR UNDERSHOOT.
C  
C
        OVERS(DPTDZ,DZTOP)=DZTOP
C
           DO 200 IT = 1,NUMTMP
C
C  CHECK THAT THERE WAS DATA FROM T-SONDE STATION (NTHTS >0)
C
              IF (NTHTS(IT).GT.0) THEN
C
C  FIND NEAREST LOW REFERENCE POINT AND THE WINDS ABOVE IT
C
		 IDIS2=(NINT(XTMP(IT))-LOWIX(1))**2+
     $                        (NINT(YTMP(IT))-LOWIY(1))**2
		 JNEAR(IT)=1
	         DO 30 JL=2,5
		   IF ((NINT(XTMP(IT))-LOWIX(JL))**2+
     $                 (NINT(YTMP(IT))-LOWIY(JL))**2 .LT. IDIS2) THEN
                      IDIS2=(NINT(XTMP(IT))-LOWIX(JL))**2+
     $                           (NINT(YTMP(IT))-LOWIY(JL))**2
                      JNEAR(IT)=JL
                   END IF
30		 CONTINUE
		 LX=LOWIX(JNEAR(IT))
		 LY=LOWIY(JNEAR(IT))
                 DO 100 L=1,NLVL
                    RHSLO(IT,L)=RHS(LX,LY,L)
                    SPD=SQRT(U(LX,LY,L)**2+V(LX,LY,L)**2)
C
C  PUT LOWER LIMIT ON POTENTIAL TEMPERATURE LAPSE RATE AND CALCULATE
C  MAXIMUM RISE CORRESPONDING TO THE WIND SPEED & LAPSE FOR THIS 
C  T-SONDE.
C
                    DTHETA=PTLAPS(IT,L)
		    IF (DTHETA .GT.-998.) THEN
                       IF (DTHETA.LE.DPOTMIN) DTHETA=DPOTMIN
                       DZMAX(IT,L)=SPD/SQRT(9.8*DTHETA/TSIGL(IT,L))
C
C   FLOW AMPLITUDES ARE LIMITED USING A STATEMENT FUNCTION (OVERS)
C   OF THE POT TMP LAPSE & TERR. AMPLITUDE.
C
                       IF (DZMAX(IT,L).GT. OVERS(DTHETA,ZRISE))
     $                            DZMAX(IT,L)=OVERS(DTHETA,ZRISE)
		    ELSE
		       DZMAX(IT,L)=-9999.
		    END IF
 100             CONTINUE
C
C  DEFINE MINIMUM ALLOWABLE FLOW SURFACE SEPARATIONS FOR EACH T-SONDE'S
C  DOMAIN OF INFLUENCE. MINIMUM SEPARATION DEFINED AS A FRACTION 
C  (CMPRES) OF THEIR SEPARATION OVER THE TERRAIN LOW POINT FOR THE 
C  T-SONDE. DO NOT LET SEPARATION BETWEEN SURFACES COMPRESS BY MORE 
C  THAN CMPRES FACTOR OR BE GREATER THAN DEFINED BY OVERS(DPTDZ,DZMAX).
C
                 DO 150 L=NLVL-1,1,-1
		    IF (DZMAX(IT,L).GT.-9998.) THEN
                       COMPDZ=CMPRES*(RHSLO(IT,L+1)-RHSLO(IT,L))
                       IF (DZMAX(IT,L).GT.(DZMAX(IT,L+1)+COMPDZ))
     $                         DZMAX(IT,L)=DZMAX(IT,L+1) +COMPDZ
		    ELSE
		       DZMAX(IT,L)=-9999.
		    END IF
 150             CONTINUE
C
C  MAKE ONE MORE PASS TO ENSURE THAT UPPER SURFACES DO NOT RISE
C  MORE RAPIDLY THAN THE SURFACE OR TERRAIN BELOW, WHICHEVER IS HIGHER.
C
                DO 155 L=2,NLVL
		   IF (DZMAX(IT,L).GT.-9998.) THEN
		      IF (L.EQ.2) THEN
                         IF (DZMAX(IT,L).GT.OVERS(DTHETA,ZRISE))
     $                             DZMAX(IT,L)=OVERS(DTHETA,ZRISE)
		      ELSE
			 DIFF=ZRISE-RHS(LX,LY,L)
                         TOTRYZ=DZMAX(IT,L-1)
                         IF (TOTRYZ .LT. DIFF) TOTRYZ=DIFF
                         IF(DZMAX(IT,L).GT.TOTRYZ)
     $                              DZMAX(IT,L)=TOTRYZ
		      END IF
		   ELSE
		      DZMAX(IT,L)=-9999.
		   END IF
 155            CONTINUE
C
	   ELSE
	      WRITE (*,*) 'NO LEVELS IN RESIG FOR SOND ',IT
	      STOP
           END IF
C
200     CONTINUE
C
C  NOW CALL FLOWHT TO DETERMINE THE WEIGHTED AVERAGE HTS FOR EACH FLOW 
C  SURFACE ABOVE EACH GRID POINT & REDEFINE LOW FLOW SFC HEIGHTS ON 
C  GRID
C
C
        CALL FLOWHT
C
        RETURN
        END

